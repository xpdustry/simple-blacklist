import com.xpdustry.toxopid.spec.ModMetadata
import com.xpdustry.toxopid.spec.ModPlatform

plugins {
  id "java"
  id "com.xpdustry.toxopid" version "4.1.2"
  id "net.kyori.indra.publishing" version "3.1.3"
}

def metadata = ModMetadata.fromJson(file("plugin.hjson"))
group = "com.xpdustry"
version = metadata.version
description = metadata.description
def _group = group.split("\\.").reverse()
def organizationId = _group[0..-2]*.toLowerCase().join("-")
def organizationName = _group[0..-2]*.capitalize().join(" ")
def organizationDomain = _group*.toLowerCase().join(".")

repositories {
  mavenCentral()
  maven { url "https://maven.${organizationDomain}/mindustry" }
  maven { url "https://www.jitpack.io" }
}

toxopid {
  compileVersion = "v${metadata.minGameVersion}"
  platforms = [ModPlatform.SERVER]
}

dependencies {
  compileOnly toxopid.dependencies.arcCore
  compileOnly toxopid.dependencies.mindustryCore
  // For SLF4MD compatibility
  compileOnly "org.slf4j:slf4j-api:2.0.16"
}

tasks.withType(JavaCompile) {
  targetCompatibility = JavaVersion.VERSION_1_8
  sourceCompatibility = JavaVersion.VERSION_1_8
  options.encoding = "UTF-8"
  options.compilerArgs.addAll(["--release", "8"])
}

jar {
  archiveFileName = "${metadata.name}.jar"
  
  // Includes plugin json and icon
  from(rootDir) {
    include "plugin.hjson"
  }
  
  //copy the builded jar to the working directory
  doLast {
		copy {
			from jar
			into rootDir
		}
	}
}

// Publishing
signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)
}

indra {
  javaVersions {
    target(8)
    minimumToolchain(8)
  }
    
  mitLicense()
  publishReleasesTo(organizationId, "https://maven.${organizationDomain}/releases")

  def repo = metadata.repository.split("/")
  github(repo[0], repo[1]) {
    ci(true)
    issues(true)
    scm(true)
  }

  configurePublications {
    from components.java
  
    pom {
      organization {
        name = organizationName
        url = "https://www.${organizationDomain}"
      }

      developers {
        metadata.author.split(",").each { a ->
          developer {
            id = a.trim()
            url = "https://github.com/${id.get()}"
          }
        }
      }
    }
  }
}
